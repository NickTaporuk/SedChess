#n
1s/.*/\
    Bin:1\
    figures!\
    board!\
    repeat?\
    estimate-black-pawn!\
    log!\
    del!\
    input!\
    step!\
    board!\
    count-pieces!\
    repeat-end!\
/
# –ø–µ—Ä–µ—Ñ–æ—Ä–º–∞—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –∫–æ–º–∞–Ω–¥
1s/ *//g; 1s/\n/ /g; 1s/^ //

# –æ–±—Ä–∞–±–æ—Ç–∫–∞ –ø–æ—Å—Ç—É–ø–∞—é—â–µ–π –∫–æ–º–∞–Ω–¥—ã
1!{
    /^[a-h][1-8] *[a-h][1-8]$/ {
        y/12345678abcdefgh/987654323579bdfh/

        # –¥–æ–±–∞–≤–ª—è–µ–º –ø–æ–ª—É—á–µ–Ω–Ω—ã–µ –∑–Ω–∞—á–µ–Ω–∏—è –≤–ø–µ—Ä–µ–¥–∏ —Å—Ç–µ–∫–∞ –∏—Å–ø–æ–ª–Ω–µ–Ω–∏—è
        G; s/\n/ /
        # –ø–µ—Ä–µ—Ö–æ–¥–∏–º –Ω–∞ –∏—Å–ø–æ–ª–Ω–µ–Ω–∏–µ –∫–æ–º–∞–Ω–¥
        b @
    }

    # –∏–≥—Ä–æ–∫ —Ö–æ—á–µ—Ç –≤—ã–π—Ç–∏
    /^q/ q

    # –≤–≤–µ–¥–µ–Ω–∞ –∫–∞–∫–∞—è-—Ç–æ –µ—Ä—É–Ω–¥–∞, —Å—Ç–∏—Ä–∞–µ–º –∏ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
    i\
    [12H[J[1A

    g
    b
}

:@

/[a-z][a-z-]*[!?]/ {
    s//@&/

    # —É–±—Ä–∞—Ç—å –≤–µ—Ä—Ö–Ω–µ–µ –∑–Ω–∞—á–µ–Ω–∏–µ
    /@del! */ {
        s///
        s/^[^ ]* //
        b @
    }

    # –ø–æ–≤—Ç–æ—Ä, –ø–æ–∫–∞ –Ω–∞ –≤–µ—Ä—à–∏–Ω–µ –Ω–µ –ø—É—Å—Ç–æ–µ
    /@repeat\? */ {
        /Bin:1* */ {
            s///
            s/@repeat\?\(.*\)repeat-end!/\1repeat\?\1repeat-end!/

            b @
        }
        s/Bin:[^ ]* *//

        b @
    }

    # –¥—É–±–ª–∏—Ä–æ–≤–∞–Ω–∏–µ –ø–µ—Ä–≤–æ–≥–æ –∑–Ω–∞—á–µ–Ω–∏—è
    /@dup! */ {
        s///
        s/[^ ]*/& &/

        b @
    }

    # –≤–≤–æ–¥ –¥–∞–Ω–Ω—ã—Ö
    /@input! */ {
        s///; h; b
    }

    # –≥–µ–Ω–µ—Ä–∞—Ü–∏—è –Ω–∞—á–∞–ª—å–Ω–æ–≥–æ —Å–æ—Å—Ç–æ—è–Ω–∏—è –¥–æ—Å–∫–∏
    /@figures! */ {
        # —Ñ–æ—Ä–º–∞—Ç: XYFig
        # –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –±–µ–ª—ã—Ö —Ç—É—Ç –∏ –¥–∞–ª—å—à–µ –¥–æ–ª–∂–Ω—ã –∏–¥—Ç–∏ –ù–ò–ñ–ï —á—ë—Ä–Ω—ã—Ö
        s//Board:\
32R52N72I92Qb2Kd2If2Nh2R\
33P53P73P93Pb3Pd3Pf3Ph3P\
34¬†54¬†74¬†94¬†b4¬†d4¬†f4¬†h4¬†\
35¬†55¬†75¬†95¬†b5¬†d5¬†f5¬†h5¬†\
36¬†56¬†76¬†96¬†b6¬†d6¬†f6¬†h6¬†\
37¬†57¬†77¬†97¬†b7¬†d7¬†f7¬†h7¬†\
38p58p78p98pb8pd8pf8ph8p\
39r59n79i99qb9kd9if9nh9r /
# –ø—Ä–æ–±–µ–ª –≤ –∫–æ–Ω—Ü–µ –Ω—É–∂–µ–Ω!

        s/\n//g

        b @
    }

    # –≤—ã–≤–æ–¥ –¥–æ—Å–∫–∏
    /@board! */ {
        s///
        # —Å–æ—Ö—Ä–∞–Ω—è–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
        h
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏
        s/.*Board://
        s/ .*$//
        # —Ä–∞—Å—à–∏—Ñ—Ä–æ–≤—ã–≤–∞–µ–º –¥–æ—Å–∫—É
        s/\([1-9a-h]\)\([1-9]\)\(.\)/[\2;\1H\3 /g
        # Pawn, Queen, King, bIshop, kNight, Rook
        y/pqkinrPQKINR/‚ôü‚ôõ‚ôö‚ôù‚ôû‚ôú‚ôô‚ôï‚ôî‚ôó‚ôò‚ôñ/

        # —Ä–∞—Å—Ü–≤–µ—á–∏–≤–∞–µ–º
        s/[2468];[37bf]H/&[48;5;209;37;1m/g
        s/[3579];[37bf]H/&[48;5;94;37;1m/g
        s/[2468];[59dh]H/&[48;5;94;37;1m/g
        s/[3579];[59dh]H/&[48;5;209;37;1m/g

        # —Ä–∞—Å—à–∏—Ñ—Ä–æ–≤—ã–≤–∞–µ–º hex
        s/b/11/g;s/d/13/g;s/f/15/g;s/h/17/g

        s/$/[0m[11H/
        # –≤—ã–≤–æ–¥–∏–º –¥–æ—Å–∫—É –∏ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –≤—Å—ë –∫–∞–∫ –±—ã–ª–æ
        i\
[2J[1;3Ha b c d e f g h\
8\
7\
6\
5\
4\
3\
2\
1\
\
Enter command:
        p
        g

        b @
    }

    # –¥–µ–ª–∞–µ–º —Ö–æ–¥ –ø–æ –≤–≤–µ–¥—ë–Ω–Ω—ã–º –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª–µ–º –¥–∞–Ω–Ω—ã–º
    /@step! */ {
        s///

        # –≥–∞—Ä–¥—ã –æ—Å–Ω–æ–≤–Ω—ã—Ö —Ä–µ–≥—É–ª—è—Ä–æ–∫ (–∏—Ö –Ω—É–∂–Ω–æ —Ç—â–∞—Ç–µ–ª—å–Ω–æ –∑–∞—â–∏—â–∞—Ç—å –æ—Ç –Ω–µ—Å—Ä–∞–±–∞—Ç—ã–≤–∞–Ω–∏–π,
        # –∏–Ω–∞—á–µ sed –≤—ã–¥–∞—Å—Ç –æ—à–∏–±–∫—É –∏ –æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—Å—è)
        # –≤—ã—á–∏—â–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏ –∏ –ø–µ—Ä–≤—ã—Ö –¥–≤—É—Ö –∑–Ω–∞—á–µ–Ω–∏–π
        h; s/\([^ ]*\) \([^ ]*\).*Board:\([^ ]*\).*/\1 \2 \3/
        
        # –≤—ã–¥–µ–ª—è–µ–º —É–∫–∞–∑–∞–Ω–Ω—ã–µ –∫–ª–µ—Ç–∫–∏
        s/\([^ ]*\) [^ ]* .*\(\1.\)/&(1:\2)/
        s/[^ ]* \([^ ]*\) .*\(\1.\)/&(2:\2)/
        # —Ç–µ–ø–µ—Ä—å –æ–Ω–∏ –∏–º–µ—é—Ç —Ñ–æ—Ä–º–∞—Ç:
        # –Ω–æ–º–µ—Ä_–ø–æ_–ø–æ—Ä—è–¥–∫—É_–≤–≤–æ–¥–∞:XY–§–∏–≥—É—Ä–∞
        s/.*(\(.....\)).*(\(.....\)).*/\1 \2/

        # —Ç–µ–ø–µ—Ä—å –Ω–∞–¥–æ –ø—Ä–æ–≤–µ—Ä–∏—Ç—å:
        # 1. —á—Ç–æ –±–µ—Ä—ë–º –Ω–µ —á—É–∂—É—é –∏ –Ω–µ –ø—É—Å—Ç—É—é —Ñ–∏–≥—É—Ä—É
        /1:..[PQKINR¬†]/ {
            g; b @
        }

        # 2. –Ω–µ –∫–ª–∞–¥—ë–º –Ω–∞ –º–µ—Å—Ç–æ —Å–≤–æ–µ–π —Ñ–∏–≥—É—Ä—ã
        /2:..[pqkbnr]/ {
            g; b @
        }

        # –µ—Å–ª–∏ —Ö–æ–¥ –±—É–¥–µ—Ç –≤–ø–µ—Ä—ë–¥‚Ä¶
        /2:.*1:/ {
            g
            /\([^ ]*\) \([^ ]*\) \(.*Board:[^ ]*\2\).\([^ ]*\1\)\([pqkbnr]\)/ {
                s//\3\5\4¬†/
                b @
            }
        }

        # —Ö–æ–¥ –Ω–∞–∑–∞–¥
        g
        s/\([^ ]*\) \([^ ]*\) \(.*Board:[^ ]*\1\)\([pqkbnr]\)\([^ ]*\2\)./\3¬†\5\4/
        b @
    }

    # –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –æ—Å—Ç–∞–≤—à–∏—Ö—Å—è —Ñ–∏–≥—É—Ä
    /@count-pieces! */ {
        s///
        h
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏
        s/.*Board://
        s/ .*$//
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –±–µ–ª—ã—Ö —Ñ–∏–≥—É—Ä
        s/[^pqkbnrPQKINR]//g
        # —Å—á–∏—Ç–∞–µ–º
        s/./1/g; s/^/Bin:/
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
        G
        # –ø–æ—Å–ª–µ G –ø–æ—è–≤–∏–ª—Å—è –ø–µ—Ä–µ–≤–æ–¥ —Å—Ç—Ä–æ–∫–∏, —É–±–∏—Ä–∞–µ–º –µ–≥–æ
        s/\n/ /

        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –∏–º–µ—é—â–∏—Ö—Å—è —á—ë—Ä–Ω—ã—Ö —Ñ–∏–≥—É—Ä
    /@estimate-black-pieces! */ {
        # –ø–µ—à–∫–∞¬†‚Äî 100, —Å–ª–æ–Ω –∏ –∫–æ–Ω—å¬†‚Äî 300, –ª–∞–¥—å—è¬†‚Äî 500, —Ñ–µ—Ä–∑—å¬†‚Äî 900

        # –æ—á–∏—Å—Ç–∫–∞ –≤—Å–µ–≥–æ –ª–∏—à–Ω–µ–≥–æ
        s///; h; s/.*Board://; s/ .*$//
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –ø–æ–¥—Å—á–∏—Ç—ã–≤–∞–µ–º—ã—Ö —Ñ–∏–≥—É—Ä
        s/[^PINRQ]//g
        # —Å—á–∏—Ç–∞–µ–º –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ * –∫–æ—ç—Ñ—Ñ–∏—Ü–∏–µ–Ω—Ç —Ñ–∏–≥—É—Ä—ã (—Ñ–µ—Ä–∑—å¬†Q ‚Äî –µ–¥–∏–Ω—Å—Ç–≤–µ–Ω–Ω—ã–π)
        s/P/1::/g; s/[IN]/111::/g; s/R/11111::/g; s/Q/111111111::/; s/^/Bin:/
        # –¥–æ–±–∞–≤–ª—è–µ–º –∫ —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω–æ–º—É —Å—Ç–µ–∫—É
        G; s/\n/ /

        b @
    }

    #–¥–ª—è –æ—Ç–ª–∞–¥–∫–∏: –≤—ã–≤–æ–¥ —Ç–µ–∫—É—â–µ–≥–æ —Å—Ç–µ–∫–∞
    /@log! */ {
        s///
        l
        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω—ã—Ö –ø–µ—à–µ–∫
    /@estimate-black-pawn! */ {
        # –æ—á–∏—Å—Ç–∫–∞ –≤—Å–µ–≥–æ –ª–∏—à–Ω–µ–≥–æ
        s///; h; s/.*Board://; s/ .*$//
        # –æ—Å—Ç–∞–≤–ª—è–µ–º —Ç–æ–ª—å–∫–æ —á—ë—Ä–Ω—ã–µ –∏ –±–µ–ª—ã–µ –ø–µ—à–∫–∏, –ø–µ—Ä–µ–∫–æ–¥–∏—Ä—É–µ–º –∏—Ö –≤ –ø–æ–Ω—è—Ç–Ω—ã–µ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã
        # —Ç–µ–ø–µ—Ä—å –ø–µ—à–∫–∏ –∑–∞–ø–∏—Å–∞–Ω—ã –≤–æ—Ç —Ç–∞–∫: X–¶–≤–µ—Ç (–≥–¥–µ –¶–≤–µ—Ç ‚Äî Black –∏–ª–∏ White), —Ä–∞–∑–¥–µ–ª–µ–Ω—ã –ø—Ä–æ–±–µ–ª–æ–º
        s/[3579bdfh][2-9][^Pp]//g; y/Pp3579bdfh/WB12345678/; s/\([1-8]\)[1-9]/ \1/g

        # ‚Üí –≠—Ç–∞–ø 1
        # –∏—â–µ–º —á—ë—Ä–Ω—ã–µ –ø–µ—à–∫–∏, –Ω–∞ –≤–µ—Ä—Ç–∏–∫–∞–ª–∏ —É –∫–æ—Ç–æ—Ä—ã—Ö —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ, –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –±–µ–ª—ã—Ö –∏–¥—É—Ç
        # –≤—Å–µ–≥–¥–∞ –ü–ï–†–ï–î –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞–º–∏ —á—ë—Ä–Ω—ã—Ö
        :estimate-black-pawn::black
        /\([1-8]\)W\(.*\1\)B/ {
            s//\1W\2b/
            b estimate-black-pawn::black
        }

        # ‚Üí –≠—Ç–∞–ø 2.1
        # –ø–µ—Ä–µ–≤–æ–¥–∏–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –≤ –ø–æ—Å–ª–µ–¥–æ–≤–∞—Ç–µ–ª—å–Ω–æ—Å—Ç–∏ –¥–ª–∏–Ω—ã X
        :estimate-black-pawn::x
        /[2-8]/ {
            s/[2-8]/1&/g
            y/2345678/1234567/

            b estimate-black-pawn::x
        }

        # ‚Üí –≠—Ç–∞–ø 2.2
        # –∏—â–µ–º –ø–µ—à–∫–∏, –Ω–µ –æ—Ç—Å–µ—è–Ω–Ω—ã–µ –Ω–∞ —ç—Ç–∞–ø–µ 1, —É –∫–æ—Ç–æ—Ä—ã—Ö –Ω–∞ —Å–æ—Å–µ–¥–Ω–µ–π –ª–∏–Ω–∏–∏ —Å–ª–µ–≤–∞ —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ
        :estimate-black-pawn::left
        /\( 1*\)W\(.*\11\)B/ {
            s//\1W\2b/
            b estimate-black-pawn::left
        }

        # ‚Üí –≠—Ç–∞–ø 2.3
        # –∏—â–µ–º –ø–µ—à–∫–∏, –Ω–µ –æ—Ç—Å–µ—è–Ω–Ω—ã–µ –Ω–∞ —ç—Ç–∞–ø–µ 2, —É –∫–æ—Ç–æ—Ä—ã—Ö –Ω–∞ —Å–æ—Å–µ–¥–Ω–µ–π –ª–∏–Ω–∏–∏ —Å–ø—Ä–∞–≤–∞ —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ
        :estimate-black-pawn::right
        / 1\(1*\)W\(.* \1\)B/ {
            s// 1\1W\2b/
            b estimate-black-pawn::right
        }

        # –í –∏—Ç–æ–≥–µ, W¬†‚Äî –±–µ–ª—ã–µ –ø–µ—à–∫–∏, b¬†‚Äî —á—ë—Ä–Ω—ã–µ, B¬†‚Äî —á—ë—Ä–Ω—ã–µ —Å–≤–æ–±–æ–¥–Ω—ã–µ –ø–µ—à–∫–∏
        # –∏–∑–±–∞–≤–ª—è–µ–º—Å—è –æ—Ç –Ω–µ—Å–≤–æ–±–æ–¥–Ω—ã—Ö –∏ –±–µ–ª—ã—Ö –ø–µ—à–µ–∫
        s/ [^ ]*[Wb]//g

        # ‚Üí –≠—Ç–∞–ø 3
        # —Å—á–∏—Ç–∞–µ–º —Å—Ç–æ–∏–º–æ—Å—Ç–∏ —á—ë—Ä–Ω—ã—Ö —Å–≤–æ–±–æ–¥–Ω—ã—Ö –ø–µ—à–µ–∫
        s/ 1B//; s/ 11B/ ::11111B/; s/ 111B/ :1:B/; s/ 1111B/ :1:11111B/; s/ 11111B/ :11:B/
        s/ 111111B/ :111:B/; s/ 1111111B/ 1:1111:B/; s/ 11111111B//

        # ‚Üí –≠—Ç–∞–ø 4
        # —Å–æ—Ö—Ä–∞–Ω—è–µ–º –ø–æ–ª—É—á–µ–Ω–Ω–æ–µ, –≥—Ä—É–∑–∏–º —Å—Ç–µ–∫ –æ–±—Ä–∞—Ç–Ω–æ, –≤—ã—Ä–µ–∑–∞–µ–º –¥–æ—Å–∫—É –∏ –æ—Å—Ç–∞–≤–ª—è–µ–º —á—ë—Ä–Ω—ã–µ –ø–µ—à–∫–∏ —Å –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞–º–∏
        G; h; s/.*Board://; s/ .*$//; s/[3579bdfh][2-9][^p]//g

        # –æ—Ü–µ–Ω–∏–≤–∞–µ–º –ø–æ–∑–∏—Ü–∏–∏ –≤—Å–µ—Ö –ø–µ—à–µ–∫
        s/.[29]p/::B/g

        s/[357dfh]3p/::1111B/g; s/[9b]3p/::B/g

        s/[3h][45]p/::111111B/g; s/[5f][45]p/::11111111B/g; s/[7d]4p/::11B/g; s/[9b]4p/:1:B/g

        s/[7d]5p/:1:11B/g; s/[9b]5p/:1:111111B/g

        s/[3h]6p/::11111111B/g; s/[5f]6p/:1:11B/g; s/[7d]6p/:1:111111B/g; s/[9b]6p/:11:1111B/g

        s/[3h][78]p/:1:11B/g; s/[5f][78]p/:1:111111B/g; s/[7d][78]p/:11:1111B/g; s/[9b][78]p/:111:11B/g

        # –≤—Å—Ç–∞–≤–ª—è–µ–º –ø—Ä–æ–±–µ–ª—ã –º–µ–∂–¥—É –æ—Ü–µ–Ω–∫–∞–º–∏
        s/B/& /g; s/^/ /

        # ‚Üí –≠—Ç–∞–ø 5
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω—ã–µ –æ—Ü–µ–Ω–∫–∏, —É–±–∏—Ä–∞–µ–º –æ—Å—Ç–∞—Ç–∫–∏ —Å—Ç–µ–∫–∞
        G; s/\n\(.*\)\n.*/ \1/

        # ‚Üí –≠—Ç–∞–ø 6
        # —Ç–µ–ø–µ—Ä—å —É –Ω–∞—Å –µ—Å—Ç—å –ø–æ–∑–∏—Ü–∏–æ–Ω–Ω—ã–µ –æ—Ü–µ–Ω–∫–∏ —Å–≤–æ–±–æ–¥–Ω—ã—Ö —á—ë—Ä–Ω—ã—Ö –ø–µ—à–µ–∫ –∏ –æ—Ü–µ–Ω–∫–∏ –≤—Å–µ—Ö —á—ë—Ä–Ω—ã—Ö –ø–µ—à–µ–∫
        # –Ω—É–∂–Ω–æ –∏—Ö —Å–ª–æ–∂–∏—Ç—å

        s/$/ :::S/

        :estimate-black-pawn::shift
        /11*B/ {
            # —Å–ª–æ–∂–µ–Ω–∏–µ –µ–¥–∏–Ω–∏—Ü
            :estimate-black-pawn::sum
            /11*B/ {
                s/\(11*\)B\(.*\)\(1*\)S/B\2\1\3S/
                s/:1111111111\(1*\)S/1:\1S/

                b estimate-black-pawn::sum
            }

            # —Å–¥–≤–∏–≥ —Ä–∞–∑—Ä—è–¥–∞
            s/:B/B/g; s/:\(1*\)S/S \1:/

            b estimate-black-pawn::shift
        }

        s/:\(1*\)S/S \1:/

        # –Ω–æ—Ä–º–∞–ª–∏–∑–∞—Ü–∏—è —á–∏—Å–ª–∞: —Å–æ—Ç–Ω–∏:–¥–µ—Å—è—Ç–∫–∏:–µ–¥–∏–Ω–∏—Ü—ã
        # –Ω–∞ —ç—Ç–æ–º —ç—Ç–∞–ø–µ –Ω–µ–æ—Ç–∫—É–¥–∞ –ø–æ—è–≤–∏—Ç—å—Å—è —Ç—ã—Å—è—á–∞–º¬†‚Äî –º–∞–∫—Å–∏–º–∞–ª—å–Ω–∞—è —Å—É–º–º–∞ 388
        s/[^:1]//g; s/:$//; s/^/Bin:/

        # –¥–æ–±–∞–≤–ª—è–µ–º –∫ —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω–æ–º—É —Å—Ç–µ–∫—É, –≤—ã—á–∏—â–∞–µ–º –Ω–∞—à –º—É—Å–æ—Ä, –∫–æ—Ç–æ—Ä—ã–π –º—ã —Å–∫–ª–∞–¥—ã–≤–∞–ª–∏ –≤—ã—à–µ¬†‚Äî
        # —Ç–∞–º –≤—Ç–æ—Ä–æ–π —Å—Ç—Ä–æ–∫–æ–π –ª–µ–∂–∞—Ç –æ—Ü–µ–Ω–∫–∏
        G; s/\n.*\n/ /

        b @
    }
}

b @